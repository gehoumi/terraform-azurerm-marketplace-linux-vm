# Arista VeloCloud SD-WAN Azure Virtual Edge

This Terraform module facilitates the deployment of an Arista VeloCloud SD-WAN Virtual Edge within the Azure ecosystem. This solution allows organizations to extend their SD-WAN fabric from remote sites into public cloud infrastructure to ensure guaranteed Service Level Agreements (SLAs) for cloud-hosted workloads.

This example focus on small branch deployment that demand throughput less than 1G, single virtual edge can be deployed in the private network (Azure vNets). For larger data center deployments that demand multi-gig throughput, Hub clustering can be deployed, but it is out of scope

## How to find an image in Marketplace

You must first obtain the following IDs: Offer, Publisher, SKU, and Version. This can be done using the Azure CLI with the provider name. For example, for Arista:

```bash
az vm image list -o table --publisher arista-networks --offer velocloud-virtual-edge --all
```

## Prerequisites

The example is intended as a reference and may need to be altered to accommodate specific environments.
Arista VeloCloud SD-WAN only supports a 2-NIC by default:
Attach Interfaces to Arista Instance 
- GE1 – WAN interface—Used to connect to the public network.
- GE2 – LAN interface—Used to connect to internal networks.
- Instance Type: Standard_DS2_v2
- Allocate Public IP and attach to GE1
- Security Groups – Allowed Ports:
      UDP 2426 –Arista Multipath Protocol
      TCP 22 – SSH Access (for Support Access)
      UDP 161 – SNMP
- Public Route Table (UDR): 0.0.0.0/0 to Internet Gateway
- Private Route Table (UDR): 0.0.0.0/0 to Virtual Appliance (Edge GE2 IP address)

## Orchestrator Pre-Configuration
Before initiating the Terraform plan, you must provision the Edge logically in the Arista VeloCloud Orchestrator:
1. Navigate to Configure > Edges and select New Edge.
2. Set the Model to Virtual Edge.
3. Crucial: Configure the Interface Settings before activation.
    - GE1: Set to "Routed", enable DHCP, and activate WAN Overlay.
    - GE2: Set to "Routed", enable DHCP, but deactivate WAN Overlay and NAT Direct Traffic.
4. Copy the Activation Key generated by the Orchestrator.

## Parameters for Azure Initial Configuration

The initial configuration can be provided using custom data.
The custom_data is a cloud-init script that will be executed on the first boot of the virtual machine.
The script configures the Arista SD-WAN Edge with the provided VCO, activation code, and other settings.
The script is in YAML format and is base64 encoded before being passed to the custom_data parameter.

The script includes the following parameters:
- vce: the configuration for the Arista SD-WAN Edge
- management_interface: false (disables the management interface)
- vco: the VCO address or hostname
- activation_code: the activation code for the Arista SD-WAN Edge
- vco_ignore_cert_errors: true (ignores certificate errors for the VCO)

## Usage

```hcl

locals {
  VCO              = "vcoxxxxx"
  ActivationKey    = "xxxxxxxxxxxx"
  IgnoreCertErrors = false
  admin_ssh_keys = [{
    public_key = "ssh-rsa AAAAB3xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx"
  }]
}

module "arista-networks-velocloud-edge" {
  source                       = "gehoumi/marketplace-linux-vm/azurerm"
  version                      = "1.0.7"
  name                         = "velocloud-edge"
  accept_marketplace_agreement = true # Accept the marketplace agreement for the image if required

  source_image_reference = {
    offer     = "velocloud-virtual-edge"
    publisher = "arista-networks"
    sku       = "velocloud_edge_6101"
    version   = "6.1.0"
  }

  size           = "Standard_D2_v2"
  admin_ssh_keys = local.admin_ssh_keys

  # The address space for the virtual network. This is a CIDR block that defines the range of IP addresses for the virtual network.
  address_space = ["10.168.0.0/16"]

  # The subnets for the virtual network. Each subnet is defined by its name and CIDR block.
  # The subnets are used to segment the network into different address spaces.
  # The subnets are defined in a map, where each entry contains the name of the subnet and its CIDR block.
  network_interfaces = {
    "if-nic1" = {
      name                            = "public-subnet-wan-ge1"
      address_prefixes                = ["10.168.0.0/24"]
      network_security_group          = "VELO_vVCE_SG"
      route_table_id                  = "rt_1"
      enable_storage_service_endpoint = true
      create_public_ip                = true
    },
    "if-nic2" = {
      name             = "private-subnet-lan-ge2"
      address_prefixes = ["10.168.1.0/24"]
      route_table_id   = "rt_2"
    }
  }

  # The following example creates a public route table and a private route table.
  # The public route table is associated with the public subnet and contains a default route to the Internet Gateway.
  # The private route table is associated with the private subnet and contains a default route to the virtual appliance (Edge GE2 IP address).

  route_tables = {
    "rt_1" = {
      name = "public-route-table"
      routes = {
        "route_ge1" = {
          address_prefix = "0.0.0.0/0"
          next_hop_type  = "Internet"
        }
      },
    },
    "rt_2" = {
      name = "private-route-table"
      routes = {
        "route_ge2" = {
          address_prefix         = "0.0.0.0/0"
          next_hop_type          = "VirtualAppliance"
          next_hop_in_ip_address = "10.168.1.4" # IP Address used for Edge LAN Interface GE2
        }
      },
    },
  }

  # The network security groups for the virtual network. Each network security group is defined by its name and a set of rules.
  # The rules are defined in a map, where each entry contains the name of the rule and its properties.
  additional_network_security_groups = {
    "VELO_vVCE_SG" = {
      name        = "VELO_vVCE_SG"
      description = "Security Group for VMware SD-WAN Edge"
      rules = {
        "Allow-SSH" = {
          source_address_prefixes      = ["0.0.0.0/0"]
          description                  = "Allow SSH access to the VMware SD-WAN Edge"
          priority                     = 100
          direction                    = "Inbound"
          access                       = "Allow"
          protocol                     = "Tcp"
          source_port_range            = "*"
          destination_port_ranges      = ["22"]
          source_address_prefixes      = ["0.0.0.0/0"]
          destination_address_prefixes = ["0.0.0.0/0"]
        },
        "Allow-UDP-2426" = {
          name                         = "Allow-UDP-2426"
          description                  = "Allow UDP 2426 for VMware Multipath Protocol"
          priority                     = 101
          direction                    = "Inbound"
          access                       = "Allow"
          protocol                     = "Udp"
          source_port_range            = "*"
          destination_port_ranges      = ["2426"]
          source_address_prefixes      = ["0.0.0.0/0"]
          destination_address_prefixes = ["0.0.0.0/0"]
        },
        "Allow-SNMP" = {
          name                         = "Allow-SNMP"
          description                  = "Allow SNMP access to the VMware SD-WAN Edge"
          priority                     = 102
          direction                    = "Inbound"
          access                       = "Allow"
          protocol                     = "Udp"
          source_port_range            = "*"
          destination_port_ranges      = ["161"]
          source_address_prefixes      = ["0.0.0.0/0"]
          destination_address_prefixes = ["0.0.0.0/0"]
        }
      }
    }
  }

  # Initial configuration 
  custom_data = base64encode(
    <<EOT
#cloud-config
velocloud:
 vce:
  management_interface: false
  vco: ${local.VCO}
  activation_code: ${local.ActivationKey}
  vco_ignore_cert_errors: ${local.IgnoreCertErrors}
EOT
  )
}
```

### Deployment Steps

* (optional) Authenticate to AzureRM and switch to the subscription of your choice if necessary.
* Initialize the Terraform module:

                  terraform init

* (optional) Plan your infrastructure to see what will be deployed:

                  terraform plan

* Deploy the infrastructure (you will have to confirm it by typing `yes`):

                  terraform apply

      The deployment takes a few minutes. Wait a few minutes for the SD-WAN Edge to bootstrap.


### Cleanup

To remove the deployed infrastructure, run:

```sh
terraform destroy
```

## Reference
<!-- BEGIN_TF_DOCS -->
## Requirements

| Name | Version |
|------|---------|
| <a name="requirement_terraform"></a> [terraform](#requirement\_terraform) | >= 1.2, < 2.0 |

## Providers

No providers.

## Modules

| Name | Source | Version |
|------|--------|---------|
| <a name="module_arista-networks-velocloud-edge"></a> [arista-networks-velocloud-edge](#module\_arista-networks-velocloud-edge) | gehoumi/marketplace-linux-vm/azurerm | 1.0.7 |

## Resources

No resources.

## Inputs

No inputs.

## Outputs

No outputs.
<!-- END_TF_DOCS -->